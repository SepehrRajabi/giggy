pub const MultiField = struct {
    meta: Meta,
    fields: []Field,

    const Self = @This();

    pub const Meta = struct {
        cid: u32,
        fields: []const Field.Meta,

        pub inline fn from(comptime T: type) Meta {
            const ti = @typeInfo(T);
            assert(ti == .@"struct");
            assert(@hasDecl(T, "cid"));
            const cid = T.cid;
            const l = ti.@"struct".fields.len;
            const fs = comptime blk: {
                var tmp: [l]Field.Meta = undefined;
                for (0..l) |i| tmp[i] = .fromStruct(T, i);
                break :blk tmp;
            };
            return .{ .cid = cid, .fields = &fs };
        }

        pub fn clone(self: *const Meta, gpa: mem.Allocator) !Meta {
            var fs = try gpa.alloc(Field.Meta, self.fields.len);
            for (self.fields, 0..) |f, i|
                fs[i] = f;
            return .{ .cid = self.cid, .fields = fs };
        }

        pub fn deinit(self: *const Meta, gpa: mem.Allocator) void {
            gpa.free(self.fields);
        }

        pub fn extract(self: *const Meta, comptime T: type, value: *const T, out: [][]const u8) void {
            const ti = @typeInfo(T);

            assert(ti == .@"struct");
            assert(@hasDecl(T, "cid"));
            assert(T.cid == self.cid);
            const fields = ti.@"struct".fields;
            assert(fields.len == self.fields.len);

            inline for (fields, 0..) |f, i| {
                const base_ptr = @intFromPtr(value);
                const offset = @offsetOf(T, f.name);
                const field_ptr = @as(*f.type, @ptrFromInt(base_ptr + offset));
                out[i] = std.mem.asBytes(field_ptr);
            }
        }
    };

    pub fn init(gpa: mem.Allocator, meta: Meta) !Self {
        var fs = try gpa.alloc(Field, meta.fields.len);
        errdefer gpa.free(fs);
        for (meta.fields, 0..) |field_meta, i| {
            fs[i] = Field.init(gpa, field_meta) catch |err| {
                for (0..i) |j|
                    fs[j].deinit(gpa);
                return err;
            };
        }
        return .{
            .meta = try meta.clone(gpa),
            .fields = fs,
        };
    }

    pub fn deinit(self: *const Self, gpa: mem.Allocator) void {
        for (self.fields) |*f|
            f.deinit(gpa);
        gpa.free(self.fields);
        self.meta.deinit(gpa);
    }

    pub fn append(self: *Self, gpa: mem.Allocator, value: anytype) !void {
        const T = @TypeOf(value);
        const ti = @typeInfo(T);
        assert(ti == .@"struct");
        const field_count = ti.@"struct".fields.len;
        assert(field_count == self.fields.len);

        var extracted: [field_count][]const u8 = undefined;
        self.meta.extract(T, &value, &extracted);

        try self.appendRaw(gpa, &extracted);
    }

    pub fn appendRaw(self: *Self, gpa: mem.Allocator, data: []const []const u8) !void {
        assert(data.len == self.fields.len);
        for (self.fields, data, 0..) |*f, d, i| {
            f.appendRaw(gpa, d) catch |err| {
                for (0..i) |j|
                    self.fields[j].pop();
                return err;
            };
        }
    }

    pub fn remove(self: *Self, index: usize) void {
        assert(index < self.len());
        for (self.fields) |*f| f.remove(index);
    }

    pub fn pop(self: *Self) void {
        self.remove(self.len() - 1);
    }

    pub fn len(self: *const Self) usize {
        const l = self.fields[0].len();
        for (self.fields[1..]) |f|
            assert(f.len() == l);
        return l;
    }
};

test "MultiField.Meta.from" {
    const C1 = struct {
        const cid = 1;
        x: u32,
        y: u32,
    };
    try testing.expectEqualDeep(
        MultiField.Meta{
            .cid = 1,
            .fields = ([_]Field.Meta{
                Field.Meta{ .index = 0, .name = "x", .size = 4, .alignment = 4 },
                Field.Meta{ .index = 1, .name = "y", .size = 4, .alignment = 4 },
            })[0..],
        },
        MultiField.Meta.from(C1),
    );
    const C2 = struct {
        const cid = 2;
        a: u8,
        b: u32,
        c: u16,
    };
    try testing.expectEqualDeep(
        MultiField.Meta{
            .cid = 2,
            .fields = ([_]Field.Meta{
                Field.Meta{ .index = 0, .name = "a", .size = 1, .alignment = 1 },
                Field.Meta{ .index = 1, .name = "b", .size = 4, .alignment = 4 },
                Field.Meta{ .index = 2, .name = "c", .size = 2, .alignment = 2 },
            })[0..],
        },
        MultiField.Meta.from(C2),
    );
}

test "MultiField.Meta.clone" {
    const alloc = testing.allocator;
    const C = struct {
        const cid = 1;
        x: u32,
        y: u32,
    };
    const meta = MultiField.Meta.from(C);
    const meta_clone = try meta.clone(alloc);
    defer meta_clone.deinit(alloc);
    try testing.expectEqualDeep(meta, meta_clone);
}

test "MultiField.Meta.extract" {
    const C = struct {
        const cid = 1;
        x: u32,
        y: u32,
    };
    const meta: MultiField.Meta = .from(C);
    var extracted: [2][]const u8 = undefined;
    meta.extract(C, &C{
        .x = 10,
        .y = 20,
    }, &extracted);
    try testing.expectEqual(
        @as(u32, 10),
        mem.bytesAsValue(u32, extracted[0]).*,
    );
    try testing.expectEqual(
        @as(u32, 20),
        mem.bytesAsValue(u32, extracted[1]).*,
    );
}

test "MultiField.appendRaw" {
    const alloc = testing.allocator;
    const C = struct {
        const cid = 1;
        x: u32,
        y: u32,
    };
    var multi = try MultiField.init(alloc, .from(C));
    defer multi.deinit(alloc);
    const data = [_][]const u8{
        &mem.toBytes(@as(u32, 0xDEADBEEF)),
        &mem.toBytes(@as(u32, 0xCAFECAFE)),
    };
    try multi.appendRaw(alloc, &data);
    try testing.expectEqual(multi.len(), 1);
    try testing.expectEqual(
        @as(u32, 0xDEADBEEF),
        mem.bytesAsValue(u32, multi.fields[0].atRaw(0)).*,
    );
    try testing.expectEqual(
        @as(u32, 0xCAFECAFE),
        mem.bytesAsValue(u32, multi.fields[1].atRaw(0)).*,
    );
}

test "MultiField.append" {
    const alloc = testing.allocator;
    const C = struct {
        pub const cid = 1;
        x: u32,
        y: u32,
    };
    var multi = try MultiField.init(alloc, .from(C));
    defer multi.deinit(alloc);
    const value = C{
        .x = 0xDEADBEEF,
        .y = 0xCAFECAFE,
    };
    try multi.append(alloc, value);
    try testing.expectEqual(multi.len(), 1);
    try testing.expectEqual(
        @as(u32, 0xDEADBEEF),
        mem.bytesAsValue(u32, multi.fields[0].atRaw(0)).*,
    );
    try testing.expectEqual(
        @as(u32, 0xCAFECAFE),
        mem.bytesAsValue(u32, multi.fields[1].atRaw(0)).*,
    );
}

const std = @import("std");
const mem = std.mem;
const testing = std.testing;
const assert = std.debug.assert;

const Field = @import("field.zig").Field;
