
pub const LOCOMOTION_ANIM_PRIORITY = 0;

pub fn updateLocomotionAnimationSystem(app: *core.App) !void {
    const room_mgr = app.getResource(level_resources.RoomManager).?;
    const current_room_id = room_mgr.current orelse return;

    var it = app.world.query(&[_]type{
        components.animation.Animation,
        components.transform.Velocity,
        components.animation.LocomotionAnimSet,
        components.animation.LocomotionAnimState,
        components.world.Room,
    });
    while (it.next()) |_| {
        const av = it.get(components.animation.AnimationView);
        const vv = it.get(components.transform.VelocityView);
        const set = it.get(components.animation.LocomotionAnimSetView);
        const state = it.get(components.animation.LocomotionAnimStateView);
        const rm = it.get(components.world.RoomView);

        if (rm.id.* != current_room_id) continue;
        if (av.priority.* > LOCOMOTION_ANIM_PRIORITY) continue;

        const speed = std.math.sqrt(vv.x.* * vv.x.* + vv.y.* * vv.y.*);
        const start = set.move_start.*;
        const stop = set.move_stop.*;
        if (!state.moving.* and speed >= start) state.moving.* = true;
        if (state.moving.* and speed <= stop) state.moving.* = false;

        const new_anim = if (state.moving.*) set.run.* else set.idle.*;
        if (new_anim != av.index.*) {
            av.index.* = new_anim;
            const base_speed = set.base_speed.*;
            const ref = @max(set.run_speed_ref.*, 0.001);
            const scale = std.math.clamp(speed / ref, set.speed_scale_min.*, set.speed_scale_max.*);
            av.speed.* = base_speed * scale;
        } else if (state.moving.*) {
            const base_speed = set.base_speed.*;
            const ref = @max(set.run_speed_ref.*, 0.001);
            const scale = std.math.clamp(speed / ref, set.speed_scale_min.*, set.speed_scale_max.*);
            av.speed.* = base_speed * scale;
        } else {
            av.speed.* = set.base_speed.*;
        }
    }
}

pub fn updateSpriteAnimationSystem(app: *core.App) !void {
    const time = app.getResource(core.Time).?;
    const assets = app.getResource(engine.assets.AssetManager).?;

    var it = app.world.query(&[_]type{
        components.render.Sprite,
        components.animation.Animation,
    });
    while (it.next()) |_| {
        const sp = it.get(components.render.SpriteView);
        const am = it.get(components.animation.AnimationView);

        const sprites = assets.sprites.getPtr(sp.name.*).?;
        const frame_count = @as(usize, sprites.len);
        const max_acc = @as(f32, @floatFromInt(frame_count)) / am.speed.*;

        am.accum.* += time.dt;
        while (am.accum.* > max_acc) : (am.accum.* -= max_acc) {}
        const new_current = @as(usize, @intFromFloat(am.accum.* * am.speed.*)) % frame_count;
        am.frame.* = new_current;
    }
}

pub fn update3DModelAnimationsSystem(app: *core.App) !void {
    const time = app.getResource(core.Time).?;
    const assets = app.getResource(engine.assets.AssetManager).?;
    var it = app.world.query(&[_]type{ components.render.Model3D, components.animation.Animation });
    while (it.next()) |_| {
        const mv = it.get(components.render.Model3DView);
        const am = it.get(components.animation.AnimationView);

        const model = assets.models.getPtr(mv.name.*).?;
        const frame_count = @as(usize, @intCast(model.animations[am.index.*].keyframeCount));
        const max_acc = @as(f32, @floatFromInt(frame_count)) / am.speed.*;

        am.accum.* += time.dt;
        while (am.accum.* > max_acc) : (am.accum.* -= max_acc) {}
        const new_current = @as(usize, @intFromFloat(am.accum.* * am.speed.*)) % frame_count;
        am.frame.* = new_current;
    }
}

pub fn renderSpritesSystem(app: *core.App) !void {
    const assets_mgr = app.getResource(engine.assets.AssetManager).?;
    const render_targets = app.getResource(resources.RenderTargets).?;
    const room_mgr = app.getResource(level_resources.RoomManager).?;
    const current_room_id = room_mgr.current orelse return;
    var it = app.world.query(&[_]type{
        components.render.Sprite,
        components.render.RenderInto,
        components.world.Room,
    });
    while (it.next()) |_| {
        const sp = it.get(components.render.SpriteView);
        const into = it.getAuto(components.render.RenderInto).into;
        const rm = it.get(components.world.RoomView);

        if (rm.id.* != current_room_id) continue;

        const index = if (it.getOrNull(components.animation.AnimationView)) |anim|
            anim.frame.*
        else
            0;
        const sprite = assets_mgr.sprites.get(sp.name.*).?;
        const render_texture = render_targets.render_textures.get(into.*).?;

        rl.BeginTextureMode(render_texture);
        rl.ClearBackground(rl.BLANK);

        if (it.getOrNull(components.render.Model3DRenderCameraView)) |cam| {
            var camera3d = DEFAULT_RENDER_CAMERA;

            camera3d.position.x = cam.pos_x.*;
            camera3d.position.y = cam.pos_y.*;
            camera3d.position.z = cam.pos_z.*;

            camera3d.target.x = cam.target_x.*;
            camera3d.target.y = cam.target_y.*;
            camera3d.target.z = cam.target_z.*;

            camera3d.fovy = cam.fovy.*;

            var plane_model = assets_mgr.models.get("plane").?;
            plane_model.model.materials[0].maps[rl.MATERIAL_MAP_DIFFUSE].texture = sprite[index];

            rl.BeginMode3D(camera3d);
            rl.DrawModel(plane_model.model, .{ .x = 0, .y = 0, .z = 0 }, 1, rl.WHITE);
            rl.EndMode3D();
        } else {
            const src = rl.Rectangle{
                .x = 0,
                .y = 0,
                .width = @floatFromInt(sprite[index].width),
                .height = @floatFromInt(-sprite[index].height),
            };
            const dst = rl.Rectangle{
                .x = 0,
                .y = 0,
                .width = @floatFromInt(render_texture.texture.width),
                .height = @floatFromInt(render_texture.texture.height),
            };
            rl.DrawTexturePro(sprite[index], src, dst, .{ .x = 0, .y = 0}, 0, rl.WHITE);
        }

        rl.EndTextureMode();
    }
}

pub fn render3DModelsSystem(app: *core.App) !void {
    const time = app.getResource(core.Time).?;
    const assets = app.getResource(engine.assets.AssetManager).?;
    const render_targets = app.getResource(resources.RenderTargets).?;
    const room_mgr = app.getResource(level_resources.RoomManager).?;
    const current_room_id = room_mgr.current orelse return;
    var it = app.world.query(&[_]type{
        components.render.Model3D,
        components.transform.Rotation,
        components.render.RenderInto,
        components.world.Room,
    });
    while (it.next()) |_| {
        const mv = it.get(components.render.Model3DView);
        const rv = it.get(components.transform.RotationView);
        const into = it.getAuto(components.render.RenderInto).into;
        const rm = it.get(components.world.RoomView);

        if (rm.id.* != current_room_id) continue;

        const rotation = interpolatedRotation(rv, time.alpha);

        const model = assets.models.getPtr(mv.name.*).?;
        const render_texture = render_targets.render_textures.get(into.*).?;

        rl.BeginTextureMode(render_texture);
        rl.ClearBackground(rl.BLANK);
        var camera3d = DEFAULT_RENDER_CAMERA;
        if (it.getOrNull(components.render.Model3DRenderCameraView)) |cam| {
            camera3d.position.x = cam.pos_x.*;
            camera3d.position.y = cam.pos_y.*;
            camera3d.position.z = cam.pos_z.*;

            camera3d.target.x = cam.target_x.*;
            camera3d.target.y = cam.target_y.*;
            camera3d.target.z = cam.target_z.*;

            camera3d.fovy = cam.fovy.*;
        }
        rl.BeginMode3D(camera3d);
        if (it.getOrNull(components.animation.AnimationView)) |am| {
            rl.UpdateModelAnimation(
                model.model,
                model.animations[am.index.*],
                @floatFromInt(am.frame.*),
            );
        }
        rl.DrawModelEx(
            model.model,
            rl.Vector3{ .x = 0, .y = 0.1, .z = 0 },
            rl.Vector3{ .x = 0, .y = 1.5, .z = 0 },
            rotation,
            rl.Vector3{ .x = 1, .y = 1, .z = 1 },
            rl.WHITE,
        );
        rl.EndMode3D();
        rl.EndTextureMode();
    }
}

pub fn renderBeginSystem(app: *core.App) !void {
    const camera_state = app.getResource(camera_resources.CameraState).?;
    rl.BeginDrawing();
    rl.ClearBackground(rl.GRAY);
    rl.BeginMode2D(camera_state.camera);
}

pub fn collectRenderablesSystem(app: *core.App) !void {
    const time = app.getResource(core.Time).?;
    const assets = app.getResource(engine.assets.AssetManager).?;
    const render_targets = app.getResource(resources.RenderTargets).?;
    const renderables_list = app.getResource(resources.Renderables).?;
    const room_mgr = app.getResource(level_resources.RoomManager).?;
    const current_room_id = room_mgr.current orelse return;
    const list = &renderables_list.list;

    // Normal env textures
    var it_texture = app.world.query(&[_]type{
        components.transform.Position,
        components.render.WidthHeight,
        components.render.Texture,
        components.world.Room,
    });
    while (it_texture.next()) |_| {
        const pos = it_texture.get(components.transform.PositionView);
        const wh = it_texture.get(components.render.WidthHeightView);
        const t = it_texture.get(components.render.TextureView);
        const z_index = if (it_texture.getAutoOrNull(components.render.ZIndex)) |z| z.value.* else 0;
        const rm = it_texture.get(components.world.RoomView);

        if (rm.id.* != current_room_id) continue;

        const texture = assets.textures.getPtr(t.name.*).?;

        try list.append(renderables_list.gpa, renderables.Renderable{
            .x = interpolatedPositionX(pos, time.alpha),
            .y = interpolatedPositionY(pos, time.alpha),
            .w = wh.w.*,
            .h = wh.h.*,
            .flip_h = false,
            .texture = texture.*,
            .z_index = z_index,
        });
    }

    // Render renderables targets
    var it_render = app.world.query(&[_]type{
        components.transform.Position,
        components.render.RenderInto,
        components.world.Room,
    });
    while (it_render.next()) |_| {
        const pos = it_render.get(components.transform.PositionView);
        const into = it_render.getAuto(components.render.RenderInto).into;
        const z_index = if (it_render.getAutoOrNull(components.render.ZIndex)) |z| z.value.* else 0;
        const rm = it_render.get(components.world.RoomView);

        if (rm.id.* != current_room_id) continue;
        const render_texture = render_targets.render_textures.get(into.*).?;

        var w = @as(f32, @floatFromInt(render_texture.texture.width));
        var h = @as(f32, @floatFromInt(render_texture.texture.height));
        if (it_render.getOrNull(components.render.WidthHeightView)) |wh| {
            w = wh.w.*;
            h = wh.h.*;
        }

        try list.append(renderables_list.gpa, renderables.Renderable{
            .x = interpolatedPositionX(pos, time.alpha) - h / 2.0,
            .y = interpolatedPositionY(pos, time.alpha) - w / 2.0,
            .w = w,
            .h = h,
            .flip_h = true,
            .texture = render_texture.texture,
            .z_index = z_index,
        });
    }
}

pub fn renderRenderablesSystem(app: *core.App) !void {
    const renderables_list = app.getResource(resources.Renderables).?;
    const list = &renderables_list.list;
    std.sort.insertion(renderables.Renderable, list.items, {}, struct {
        fn lessThan(_: void, a: renderables.Renderable, b: renderables.Renderable) bool {
            if (a.z_index != b.z_index) return a.z_index < b.z_index;
            return a.y + a.h < b.y + b.h;
        }
    }.lessThan);
    for (list.items) |r| {
        const flip: c_int = if (r.flip_h) -1 else 1;
        const src = rl.Rectangle{
            .x = 0,
            .y = 0,
            .width = @floatFromInt(r.texture.width),
            .height = @floatFromInt(r.texture.height * flip),
        };
        const dst = rl.Rectangle{
            .x = r.x,
            .y = r.y,
            .width = r.w,
            .height = r.h,
        };
        rl.DrawTexturePro(r.texture, src, dst, .{ .x = 0, .y = 0}, 0, rl.WHITE);
        // rl.DrawTextureRec(r.texture, src, .{ .x = r.x, .y = r.y }, rl.WHITE);
    }
}

pub fn renderEndMode2DSystem(app: *core.App) !void {
    _ = app;
    rl.EndMode2D();
}

pub fn renderEndSystem(app: *core.App) !void {
    _ = app;
    rl.EndDrawing();
}

pub fn clearRenderablesSystem(app: *core.App) !void {
    const renderables_list = app.getResource(resources.Renderables).?;
    renderables_list.list.clearRetainingCapacity();
}

const DEFAULT_RENDER_CAMERA = rl.Camera3D{
    .position = .{ .x = 6.0, .y = 7.0, .z = 6.0 },
    .target = .{ .x = 0.0, .y = 2.0, .z = 0.0 },
    .up = .{ .x = 0.0, .y = 1.0, .z = 0.0 },
    .fovy = 4,
    .projection = rl.CAMERA_ORTHOGRAPHIC,
};

fn interpolatedPositionX(pos: components.transform.PositionView, alpha: f32) f32 {
    return engine.math.lerp(pos.prev_x.*, pos.x.*, alpha);
}

fn interpolatedPositionY(pos: components.transform.PositionView, alpha: f32) f32 {
    return engine.math.lerp(pos.prev_y.*, pos.y.*, alpha);
}

fn interpolatedRotation(rot: components.transform.RotationView, alpha: f32) f32 {
    return engine.math.lerpAngleDeg(rot.prev_teta.*, rot.teta.*, alpha);
}

pub const LabelRenderPrepass = "render.prepass";
pub const LabelRenderBegin = "render.begin";
pub const LabelRenderPass = "render.pass";
pub const LabelRenderEndMode2D = "render.end_mode_2d";
pub const LabelRenderOverlay = "render.overlay";
pub const LabelRenderEnd = "render.end";

pub const CollectRenderablesSystemId = "render.collect";
pub const RenderRenderablesSystemId = "render.renderables";

const std = @import("std");

const engine = @import("engine");
const core = engine.core;
const rl = engine.raylib;

const game = @import("game");
const components = game.components;

const resources = game.plugins.render.resources;
const camera_resources = game.plugins.camera.resources;
const level_resources = game.plugins.level.resources;
const renderables = @import("renderables.zig");
