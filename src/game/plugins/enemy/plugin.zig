pub const Plugin = struct {
    pub fn build(self: @This(), app: *core.App) !void {
        _ = self;
        _ = try app.insertResource(resources.ChasePath, .init(app.gpa));
        const render_targets = app.getResource(render_resources.RenderTargets).?;
        const rt = try render_targets.load(96, 96);
        const assets_mgr = app.getResource(engine.assets.AssetManager).?;

        const loco_animset = blk: {
            const val = assets_mgr.configValuePath(
                "animations",
                &.{ "locomotion", "skull" },
            ).?;
            break :blk try json.parseFromValue(components.animation.LocomotionAnimSet, app.gpa, val, .{});
        };
        defer loco_animset.deinit();

        const skull_animset = blk: {
            const val = assets_mgr.configValuePath(
                "animations",
                &.{ "skull" },
            ).?;
            break :blk try json.parseFromValue(components.animation.SkullAnimSet, app.gpa, val, .{});
        };
        defer skull_animset.deinit();

        const render_camera = blk: {
            const val = assets_mgr.configValuePath(
                "render_camera",
                &.{ "skull" },
            ).?;
            break :blk try json.parseFromValue(components.render.Model3DRenderCamera, app.gpa, val, .{});
        };

        defer render_camera.deinit();

        _ = try app.world.spawn(.{
            components.enemy.Enemy{ .id = 1, .speed = 180.0, .state = .chase },
            components.transform.Position{ .x = 750, .y = 400, .prev_x = 200, .prev_y = 200 },
            components.transform.Velocity{ .x = 0, .y = 0 },
            components.collision.ColliderCircle{ .radius = 16.0, .mask = 1 },
            components.transform.Rotation{ .teta = 0, .prev_teta = 0, .target_teta = 0, .turn_speed_deg = 360.0 * 2 },
            components.render.Model3D{ .name = "skull", .render_texture = 0, .mesh = 0, .material = 1 },
            components.render.RenderInto{ .into = rt },
            components.animation.Animation{ .index = 0, .frame = 0, .accum = 0, .speed = 0, .priority = 0 },
            loco_animset.value,
            skull_animset.value,
            render_camera.value,
            components.animation.LocomotionAnimState{ .moving = false },
            level_resources.roomFromName("level1"),
        });

        try app.addSystem(.fixed_update, systems.enemyAISystem, .{
            .provides = &.{"input", "ai"},
        });
        try app.addSystem(.update, systems.updateAnimationSystem, .{
            .provides = &.{"animation"},
            .after_all_labels = &.{"ai"},
        });
    }
};

const engine = @import("engine");
const core = engine.core;
const std = @import("std");
const json = std.json;

const game = @import("game");
const components = game.components;
const level_resources = game.plugins.level.resources;
const render_resources = game.plugins.render.resources;
const systems = game.plugins.enemy.systems;
const resources = game.plugins.enemy.resources;
